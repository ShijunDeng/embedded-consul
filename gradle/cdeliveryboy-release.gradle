//Continuous Delivery release configuration with CDeliveryBoy

apply plugin: 'info.solidsoft.cdeliveryboy'
apply plugin: 'io.codearte.nexus-upload-staging'

cDeliveryBoy {
    tasks {
        uploadArchivesTask = 'uploadArchivesStaging'
        promoteRepositoryTask = 'releaseRepository'
    }
    nexus {
        autoPromote = true
    }
}

nexusStaging {
    packageGroup = 'com.pszymczyk'
    stagingProfileId = '1ebbe7fabed00a'
}

scmVersion {
    versionIncrementer 'incrementPatch'
    hooks {
        pre 'fileUpdate', [file       : 'README.md', pattern: { previousVersion, context -> /testImplementation\ 'com\.pszymczyk\.consul:embedded-consul:$previousVersion'/ },
                           replacement: { currentVersion, context -> /testImplementation 'com.pszymczyk.consul:embedded-consul:$currentVersion'/ }]
        pre 'fileUpdate', [file       : 'README.md', pattern: { previousVersion, context -> /<version>$previousVersion<\/version>/ },
                           replacement: { currentVersion, context -> /<version>$currentVersion<\/version>/ }]
        pre 'fileUpdate', [file       : 'CHANGELOG.md', pattern: { previousVersion, context -> /^##\ ${context.currentVersion}\ -\ Unreleased$/ },
                           replacement: { currentVersion, context -> /## $currentVersion - ${new Date().format( 'yyyy-MM-dd' )}/ }]
    }
}

group = 'com.pszymczyk.consul'
//Beware: All release/version related changes should be put before that line which triggers (lazy) version evaluation
project.version = scmVersion.version

String repoSlug = 'pszymczyk/embedded-consul'
modifyPom {
    project {
        name 'Embedded Consul'
        description 'Embedded Consul provides easy way to run Consul in integration tests.'
        url 'https://github.com/pszymczyk/embedded-consul'
        inceptionYear '2017'

        scm {
            url "https://github.com/${repoSlug}"
            connection "scm:git:git://github.com/${repoSlug}.git"
            developerConnection "scm:git:git@github.com:${repoSlug}.git"
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'pszymczyk'
                name 'Pawel Szymczyk'
                email 'pawel@pszymczyk.com'
                roles {
                    role 'despot'
                    role 'developer'
                }
            }
        }
    }
}

if (hasProperty('changelogSync')) {

    task syncChangelog(type: Exec) {
        doFirst { logger.info("Synchronizing changelog with GitHub for version ${project.version}") }
        commandLine 'chandler', 'push', "release/${project.version}", '--tag-prefix=release/'
    }

    syncChangelog {
        onlyIf { ciBuild.inReleaseMode }
        mustRunAfter releaseRepository
        if (project.tasks.findByName("publishPlugins")) {
            mustRunAfter publishPlugins
        }
    }
    ciBuild.dependsOn syncChangelog
}


//Some workarounds on CDeliveryBoy limitations - #3 and #13 (see below)

//Note. Referring non built-in types in external build script is problematic: https://github.com/gradle/gradle/issues/1262
//Note2. Task dependency removal is problematic due to: https://github.com/gradle/gradle/pull/6143/
["uploadArchives", "createRepository", "pointUploadArchivesToExplicitRepository"].each { String taskName ->
    tasks.named(taskName).configure {
        onlyIf {
            boolean onlyIfValue = resolveOnlyIfValueForUploadTasks()
            if (!onlyIfValue) {
                logger.lifecycle("Task disabled due to environment settings for ciBuild")
            }
            return onlyIfValue
        }
    }
}

boolean resolveOnlyIfValueForUploadTasks() {
    return !isSnapshotBuildWithSkipReleaseOnCI() && !isExtraBuildOfReleaseTagOnCI()
}
//Workaround on https://github.com/szpak/CDeliveryBoy/issues/3 - do not upload snapshots from other Java versions on CI server
boolean isSnapshotBuildWithSkipReleaseOnCI() {
    return (project.version as String).endsWith("-SNAPSHOT") && System.env.SKIP_RELEASE == "true" && isCiBuildTaskCalled()
}
//Workaround on https://github.com/szpak/CDeliveryBoy/issues/13 - do not upload artifacts on extra build on CI server (e.g. from cron) of release tag
boolean isExtraBuildOfReleaseTagOnCI() {
    return !(project.version as String).endsWith("-SNAPSHOT") && !ciBuild.inReleaseMode && isCiBuildTaskCalled()
}
boolean isCiBuildTaskCalled() {
    //It covers only basic (idiomatic) case that "ciBuild" task has been called directly. Examining taskGraph would be better, however, more problematic due to late preparation.
    return gradle.startParameter.taskNames.contains("ciBuild")
}
